def props = new Properties()
file("../local.properties").withInputStream {
    stream -> props.load(stream)
}

repositories {
    maven {
        url new File(props['sdk.dir'] + "/extras/android/m2repository/").toURI()
    }
}

apply plugin: 'groovy'

def simpleProject = project(":getstarted")

println simpleProject.buildDir

dependencies {
    compile "org.codehaus.groovy:groovy-all:1.8.6"
    compile 'org.robospock:robospock:0.5.0'
    compile 'cglib:cglib-nodep:3.1'

    testCompile "org.mockito:mockito-all:1.9.5"


    compile simpleProject
    compile fileTree(dir: simpleProject.file("build/libs"), include: "*.jar")

    // aar depedency
    compile fileTree(dir: simpleProject.file('build/intermediates/exploded-aar/com.android.support/appcompat-v7/20.0.0'), include: ['*.jar'])
    compile fileTree(dir: simpleProject.file('build/intermediates/exploded-aar/com.android.support/support-v4/20.0.0'), include: ['*.jar'])
}

//apply plugin: 'robospock'

sourceSets.test.java.srcDirs += [simpleProject.buildDir.path + '/generated/source/r/debug']

test {
    // set heap size for the test JVM(s)
    minHeapSize = "128m"
    maxHeapSize = "512m"

    // set JVM arguments for the test JVM(s)
    jvmArgs '-XX:MaxPermSize=256m'

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
//        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }

    testLogging {
        lifecycle {
            exceptionFormat "full"
        }
    }

    systemProperty 'android.manifest', '../getstarted/src/main/AndroidManifest.xml'
    systemProperty 'android.resources', '../getstarted/build/intermediates/res/release'
    systemProperty 'android.assets', '../getstarted/build/intermediates/assets/release'
}

tasks["test"].dependsOn simpleProject.tasks["zip2jar"]
